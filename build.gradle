group 'blog.automationhacks.contracttesting'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath("org.thymeleaf:thymeleaf:3.0.8.RELEASE")
        classpath("au.com.dius:pact-jvm-provider-gradle:4.0.3")
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        implementation("org.springframework.boot:spring-boot-starter-web")
        implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
        implementation("org.springframework.boot:spring-boot-devtools")
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation("junit:junit")
        testImplementation("au.com.dius:pact-jvm-consumer-junit:4.0.3")
        testImplementation("au.com.dius:pact-jvm-consumer-java8:4.0.3")
    }
}

project(':example-consumer') {
    version '1.0.0'
    apply plugin: 'au.com.dius.pact'

    jar {
        archiveBaseName = 'example-consumer'
        archiveVersion =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/pacts"
    }

    pact {
        publish {
            pactDirectory = "$rootDir/pact"

//            pactBrokerUrl = mybrokerUrl
//            pactBrokerUsername = mybrokerUser
//            pactBrokerPassword = mybrokerPassword

            // This is to local Pact Broker in Docker
            pactBrokerUrl = "http://localhost"
        }
    }
}

task copyPactToProvider(type: Copy) {
    from "${project(':example-consumer').buildDir}/pacts"
    into "${project(':example-provider').buildDir}/pacts"
}

task publishPact(dependsOn: [copyPactToProvider],
        description: 'Publish the pact file to the provider projects')

project(':example-provider') {
    apply plugin: 'au.com.dius.pact'

    jar {
        archiveBaseName = 'example-provider'
        archiveVersion =  '1.0.0'
    }

    pact {
        serviceProviders {
            ExampleProvider {
                protocol = 'http'
                host = 'localhost'
                port = 8081
                path = '/'

                // Test Pacts from local provider
                hasPactWith('example-consumer') {
                    pactSource = file("$rootDir/pacts")
                }

                // Test Pacts from Pact Broker
//                hasPactsFromPactBroker(mybrokerUrl, authentication: ['Basic', mybrokerUser, mybrokerPassword])

                // This is for local Pact Broker in Docker
                //hasPactsFromPactBroker("http://localhost")
            }
        }
    }

}


